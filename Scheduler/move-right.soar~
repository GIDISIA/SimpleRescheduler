sp {Scheduler*propose*move-right
   (state <s> ^name Scheduler
              ^resource <r1>
              ^calculations.pending-general-calculations 0)
   (<r1> ^task <t1> {<> <t1> <t2>})
   (<t1> ^name <namet1>)
   (<t2> ^name <namet2>
         ^name <> TaskFinal)
   (<t1> ^next <namet2>)
   (<t1> ^isFocal 1)
-->
   (<s> ^operator <op> +)
   (<op> ^name move-right
         ^focalTask <namet1>
         ^auxTask <namet2>)
}

sp {Scheduler*apply*move-right
   (state <s> ^name Scheduler
              ^operator <op>
              ^calculations <c>
              ^resource <r1>)
   (<c> ^pending-general-calculations <pgc>)
   (<op> ^name move-right
         ^focalTask <nametFocal>
         ^auxTask <nametAux>)
   (<r1> ^task <tFocal> {<> <tFocal> <tAux>}
                        {<> <tAux> <> <tFocal> <tPosAux>}
                        {<> <tAux> <> <tFocal> <> <tPosAux> <tPrevFocal>})
   (<tFocal> ^name <nametFocal>
         ^previous <prevt1>
         ^next <nextt1>)
   (<tAux> ^name <nametAux>
            ^previous <prevt2>
            ^next <nextt2>)
   (<tPrevFocal> ^name <nametPrevFocal> 
            ^next <nametFocal>)
   (<tPosAux> ^name <nametPosAux>
            ^previous <nametAux>)
-->
   #(write (crlf) |Aplique move-right: |<nametFocal> <nametAux> <nametPosAux> <nametPrevFocal> )
   (<tFocal> ^previous <nametAux> <prevt1> -
             ^next <nextt2> <nextt1> -)
   (<tAux> ^previous <prevt1> <prevt2> -
            ^next <nametFocal> <nextt2> -)
   (<tPrevFocal> ^next <nametAux> <nametFocal> -)
   (<tPosAux> ^previous <nametFocal> <nametAux> -)
   (<c> ^pending-general-calculations 1 <pgc> -)
}